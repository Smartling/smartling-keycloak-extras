/*
 * Copyright 2015 Smartling, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.netflix.nebula:gradle-extra-configurations-plugin:3.0.3"
        classpath "com.netflix.nebula:nebula-bintray-plugin:3.5.2"
    }
}

group = 'com.smartling.keycloak.extras'
description = 'Smartling Keycloak Extras'
version = '0.6.0' + (project.hasProperty('release') && project.ext.release ? '-RELEASE' : '-SNAPSHOT')

ext {
    springVersion = '3.2.6.RELEASE'
    springSecurityVersion = '3.2.0.RELEASE'
    keycloakVersion = '1.9.0.Final'
}

project('keycloak-spring-security-auth') {
    description = "Keycloak Spring Security Authentication Extras"
}

subprojects {
    apply plugin: "nebula.provided-base"
    apply plugin: "nebula.nebula-bintray"
    apply plugin: 'java'

    group = rootProject.group
    version = rootProject.version

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://repo.fugu.io/artifactory/ext-snapshot-local' }
    }

    dependencies {
        compile "org.slf4j:slf4j-api:1.7.7"
        compile "org.keycloak:keycloak-spring-security-adapter:${keycloakVersion}"
        provided "javax.servlet:javax.servlet-api:3.0.1"

        testRuntime "org.jboss.logging:jboss-logging:3.2.1.Final"
        testRuntime "com.fasterxml.jackson.core:jackson-databind:2.5.3"

        testCompile 'junit:junit:4.12'
        testCompile 'org.mockito:mockito-all:1.10.19'
        testCompile 'org.powermock:powermock-mockito-release-full:1.6.2'
        testCompile "org.springframework:spring-test:${springVersion}"
    }

    javadoc {
        source = sourceSets.main.allJava
        classpath = sourceSets.main.compileClasspath
        group = 'Documentation'
        description = 'Generates aggregated Javadoc API documentation.'
        title = "${rootProject.description} ${version} API"
        options.author = true
        options.header = rootProject.description
        options.links = [
                'http://docs.oracle.com/javase/7/docs/api/',
                'http://docs.spring.io/spring/docs/3.2.x/javadoc-api/',
                'http://docs.spring.io/spring-security/site/docs/3.2.x/apidocs/',
                'http://docs.jboss.org/keycloak/docs/1.2.0.CR1/javadocs/'
        ]
    }

    bintray {
        user = project.ext.bintrayUser
        key = project.ext.bintrayKey
        pkg {
            repo = 'release'
            userOrg = 'smartling'
            name = "${project.name}"
            desc = "${project.description}"
            websiteUrl = 'https://github.com/Smartling'
            issueTrackerUrl = 'https://github.com/Smartling/spring-security-keycloak/issues'
            vcsUrl = 'https://github.com/Smartling/spring-security-keycloak.git'
            licenses = ['Apache-2.0']
            labels = ['keycloak', 'spring-security']
            publicDownloadNumbers = false
            version {
                name = "${project.version}"
                desc = "${project.description}"
                released = new Date()
                vcsTag = "v${project.version.replace('-RELEASE', '')}"
            }
        }
    }
}

project('keycloak-spring-security-user-details') {
    description = "Keycloak Spring Security User Details Authentication Extras"
    dependencies {
        compile project(':keycloak-spring-security-auth')
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.8.1'
}
